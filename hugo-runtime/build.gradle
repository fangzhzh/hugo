import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

dependencies {
  compile 'org.aspectj:aspectjrt:1.8.6'
  compile project(':hugo-annotations')

  testCompile 'junit:junit:4.12'
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

android.libraryVariants.all { variant ->
  JavaCompile javaCompile = variant.javaCompile
  javaCompile.doLast {
    String[] args = [
        "-showWeaveInfo",
        "-1.5",
        "-inpath", javaCompile.destinationDir.toString(),
        "-aspectpath", javaCompile.classpath.asPath,
        "-d", javaCompile.destinationDir.toString(),
        "-classpath", javaCompile.classpath.asPath,
        "-bootclasspath", android.bootClasspath.join(File.pathSeparator)
    ]

    MessageHandler handler = new MessageHandler(true);
    new Main().run(args, handler)

    def log = project.logger
    for (IMessage message : handler.getMessages(null, true)) {
      switch (message.getKind()) {
        case IMessage.ABORT:
        case IMessage.ERROR:
        case IMessage.FAIL:
          log.error message.message, message.thrown
          break;
        case IMessage.WARNING:
        case IMessage.INFO:
          log.info message.message, message.thrown
          break;
        case IMessage.DEBUG:
          log.debug message.message, message.thrown
          break;
      }
    }
  }
}
uploadArchives {

    repositories {
        Properties props = new Properties()
        props.load(new FileInputStream(project.file("local.properties")))

        // the maven plugin features this deployer
        mavenDeployer {
            // we deploy to the release repository in this case
            println properties
            repository(url: "http://maven.dev.beetalkmobile.com/nexus/content/repositories/releases") {
                authentication(userName: props.get("nexus.user"), password: props.get("nexus.password"))
            }
            snapshotRepository(url: "http://maven.dev.beetalkmobile.com/nexus/content/repositories/snapshots") {
                // values as variable names declared in ~/.gradle/gradle.properties
                authentication(userName: props.get("nexus.user"), password: props.get("nexus.password"))
            }
            pom.project {
                groupId = 'com.jakewharton.hugo'
                artifactId = 'hugo-runtime'
                name = 'Hugo Runtime'
                packaging = 'aar'
                version = '1.2.2.1-SNAPSHOT'
            }
        }
    }
}